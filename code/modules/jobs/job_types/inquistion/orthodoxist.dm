/datum/job/orthodoxist
	title = JOB_SACRESTANTS
	department_flag = INQUISITION
	factions = list(FACTION_TOWN)
	total_positions = 99
	spawn_positions = 99
	allowed_races = RACES_LESS_DISCRIMINATED
	bypass_lastclass = TRUE
	cmode_music = 'sound/music/cmode/church/CombatInquisitor2.ogg'
	allowed_patrons = list(/datum/patron/divine/centrist, /datum/patron/angros)

	tutorial = "A fervent believer in the cause of the Inquisition. Recruited by the Inquisitor to further the Katholikon's goal in the locale."
	selection_color = JCOLOR_INQUISITION

	outfit = null
	outfit_female = null

	job_flags = (JOB_ANNOUNCE_ARRIVAL | JOB_SHOW_IN_CREDITS | JOB_EQUIP_RANK | JOB_NEW_PLAYER_JOINABLE)
	display_order = JDO_ORTHODOXIST
	job_bitflag = BITFLAG_CHURCH

	advclass_cat_rolls = list(CTAG_INQUISITION = 20)
	same_job_respawn_delay = 30 MINUTES
	antag_role = /datum/antagonist/purishep

	mind_traits = list(
		TRAIT_KNOW_INQUISITION_DOORS
	)
	languages = list(/datum/language/newunsundered)

	exp_type = list(EXP_TYPE_INQUISITION)
	exp_types_granted = list(EXP_TYPE_INQUISITION, EXP_TYPE_COMBAT)
	exp_requirements = list(
		EXP_TYPE_INQUISITION = 120
	)

/datum/job/orthodoxist/after_spawn(mob/living/carbon/human/spawned, client/player_client)
	. = ..()

	add_verb(spawned, /mob/living/carbon/human/proc/suspect_heretics)
	add_verb(spawned, /mob/living/carbon/human/proc/torture_victim)
	add_verb(spawned, /mob/living/carbon/human/proc/faith_test)
	add_verb(spawned, /mob/living/carbon/human/proc/view_inquisition)

	var/holder = spawned.patron?.devotion_holder
	if(holder)
		var/datum/devotion/devotion = new holder()
		devotion.make_templar()
		devotion.grant_to(spawned)

	spawned.hud_used?.shutdown_bloodpool()
	spawned.hud_used?.initialize_bloodpool()
	spawned.hud_used?.bloodpool.set_fill_color("#dcdddb")
	spawned.hud_used?.bloodpool?.name = "Aspects' Grace: [spawned.bloodpool]"
	spawned.hud_used?.bloodpool?.desc = "Devotion: [spawned.bloodpool]/[spawned.maxbloodpool]"
	spawned.maxbloodpool = 1000
	spawned.AddComponent(/datum/component/bloodpool_regen, 0.5)

	var/datum/species/species = spawned.dna?.species
	if(species)
		species.native_language = "Old Unsundered"
		species.accent_language = species.get_accent(species.native_language)

/datum/job/orthodoxist/remove_job(mob/living/carbon/human/spawned)
	. = ..()
	if(.)
		spawned.hud_used?.shutdown_bloodpool()
		spawned.maxbloodpool = initial(spawned.maxbloodpool)
		qdel(spawned.GetComponent(/datum/component/bloodpool_regen))

/datum/job/advclass/sacrestant
	exp_types_granted = list(EXP_TYPE_INQUISITION, EXP_TYPE_COMBAT)
	factions = list(FACTION_INQUISITION, FACTION_TOWN, FACTION_CHURCH)
