/datum/attribute_holder/sheet/job/absolver
	raw_attribute_list = list(
		STAT_ENDURANCE = 3,
		STAT_SPEED = -2,
		STAT_CONSTITUTION = 7,
		/datum/attribute/skill/misc/athletics = 30,
		/datum/attribute/skill/misc/climbing = 40,
		/datum/attribute/skill/misc/sewing = 30,
		/datum/attribute/skill/misc/reading = 30,
		/datum/attribute/skill/combat/unarmed = 10,
		/datum/attribute/skill/misc/medicine = 30,
		/datum/attribute/skill/craft/cooking = 30,
		/datum/attribute/skill/labor/fishing = 30,
		/datum/attribute/skill/misc/swimming = 30,
		/datum/attribute/skill/craft/crafting = 30,
		/datum/attribute/skill/magic/holy = 50,
		/datum/attribute/skill/combat/axesmaces = 20, //Absolver here isn't enforced Pacifist because it's extremely unfun. Still not great at combat though, but you should be able to at least defend yourself if you're in a thing called the Inquisition.
		/datum/attribute/skill/combat/shields = 20,  //To protect themselves.
	)

/datum/job/absolver
	title = JOB_ABSOLVER
	department_flag = INQUISITION
	factions = list(FACTION_INQUISITION, FACTION_TOWN, FACTION_CHURCH)
	job_flags = (JOB_ANNOUNCE_ARRIVAL | JOB_SHOW_IN_CREDITS | JOB_EQUIP_RANK | JOB_NEW_PLAYER_JOINABLE)
	total_positions = 1 // THE ONE.
	spawn_positions = 1
	allowed_races = RACES_LESS_DISCRIMINATED
	allowed_patrons = list(/datum/patron/divine/centrist, /datum/patron/angros)
	tutorial = "The Inquisitor's right hand, you serve as the cleric to the Katholikon's Inquisition, providing miracles to bolster and aid, and to serve as the spiritual leader to your flock. Ensure the word of the Elementals is followed, and there will be no cause for upset."
	selection_color = JCOLOR_INQUISITION
	outfit = /datum/outfit/absolver
	bypass_lastclass = TRUE
	display_order = JDO_ABSOLVER
	give_bank_account = 15
	knows_the_town = TRUE
	known_by_the_town = TRUE
	cmode_music = 'sound/music/cmode/church/CombatInquisitor.ogg'
	antag_role = /datum/antagonist/purishep

	job_bitflag = BITFLAG_CHURCH

	mind_traits = list(
		TRAIT_KNOW_INQUISITION_DOORS
	)
	traits = list(
		TRAIT_NOPAINSTUN,
		TRAIT_EMPATH,
		TRAIT_CRITICAL_RESISTANCE,
		TRAIT_STEELHEARTED,
		TRAIT_INQUISITION,
		TRAIT_SILVER_BLESSED,
		TRAIT_FOREIGNER,
		TRAIT_ANGROSIAN_GRIT,
	)

	spells = list(
		/datum/action/cooldown/spell/angroslux_tamper,
		/datum/action/cooldown/spell/angrosabsolve,
		/datum/action/cooldown/spell/diagnose,
	)

	attribute_sheet = /datum/attribute_holder/sheet/job/absolver

	languages = list(/datum/language/oldunsundered, /datum/language/newunsundered)

	exp_type = list(EXP_TYPE_INQUISITION)
	exp_types_granted = list(EXP_TYPE_INQUISITION)
	exp_requirements = list(
		EXP_TYPE_INQUISITION = 600
	)


// REMEMBER FLAGELLANT? REMEMBER LASZLO? THIS IS HIM NOW. FEEL OLD YET?

/datum/job/absolver/after_spawn(mob/living/carbon/human/spawned, client/player_client)
	. = ..()
	GLOB.inquisition.add_member_to_school(spawned, "Sanctae", 0, "Absolver")
	spawned.add_chem_effect(CE_PAINKILLER, 10, "[type]")

	add_verb(spawned, /mob/living/carbon/human/proc/view_inquisition)

	var/holder = spawned.patron?.devotion_holder
	if(holder)
		var/datum/devotion/devotion = new holder()
		devotion.make_absolver()
		devotion.grant_to(spawned)

	spawned.hud_used?.shutdown_bloodpool()
	spawned.hud_used?.initialize_bloodpool()
	spawned.hud_used?.bloodpool.set_fill_color("#dcdddb")
	spawned.hud_used?.bloodpool?.name = "Aspects' Grace: [spawned.bloodpool]"
	spawned.hud_used?.bloodpool?.desc = "Devotion: [spawned.bloodpool]/[spawned.maxbloodpool]"
	spawned.maxbloodpool = 1000
	spawned.AddComponent(/datum/component/bloodpool_regen, 0.5)

	var/datum/species/species = spawned.dna?.species
	if(!species)
		return
	species.native_language = "Old Unsundered"
	species.accent_language = species.get_accent(species.native_language)

/datum/job/absolver/remove_job(mob/living/carbon/human/spawned)
	. = ..()
	if(.)
		spawned.hud_used?.shutdown_bloodpool()
		spawned.maxbloodpool = initial(spawned.maxbloodpool)
		qdel(spawned.GetComponent(/datum/component/bloodpool_regen))

/datum/outfit/absolver
	name = JOB_ABSOLVER
	wrists = /obj/item/clothing/wrists/bracers/psythorns
	gloves = /obj/item/clothing/gloves/leather/otavan/inqgloves
	beltr = /obj/item/flashlight/flare/torch/lantern/psycenser
	beltl = /obj/item/weapon/mace/cudgel/psy
	cloak = /obj/item/clothing/cloak/absolutionistrobe
	backr = /obj/item/storage/backpack/satchel/otavan
	backl = /obj/item/weapon/shield/tower/metal
	belt = /obj/item/storage/belt/leather
	pants = /obj/item/clothing/pants/trou/leather/advanced/colored/duelpants
	armor = /obj/item/clothing/armor/cuirass/angros
	shirt = /obj/item/clothing/armor/gambeson/heavy/inq
	shoes = /obj/item/clothing/shoes/angrosboots
	mask = /obj/item/clothing/head/helmet/blacksteel/psythorns
	head = /obj/item/clothing/head/helmet/heavy/absolver
	ring = /obj/item/clothing/ring/signet/psy
	backpack_contents = list(
		/obj/item/natural/bundle/cloth = 2,
		/obj/item/reagent_containers/glass/bottle/healthpot/labelled = 2,
		/obj/item/paper/inqslip/arrival/abso = 1,
		/obj/item/needle = 1,
		/obj/item/storage/belt/pouch/coins/rich = 1,
		/obj/item/storage/keyring/inquisitor = 1,
		)

/datum/outfit/absolver/pre_equip(mob/living/carbon/human/equipped_human, visuals_only)
	. = ..()
	switch(equipped_human.patron?.type)
		if(/datum/patron/divine/centrist)
			neck = /obj/item/clothing/neck/psycross/silver/divine
		if(/datum/patron/angros)
			neck = /obj/item/clothing/neck/psycross/silver
